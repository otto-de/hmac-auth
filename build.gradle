apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

// for gradle plugins
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.1'
        classpath 'org.codehaus.groovy:groovy-backports-compat23:3.0.8'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'de.otto'
    version = '2.3.8-SNAPSHOT'
    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    jar {
        manifest.attributes provider: 'gradle'
    }

    test {
        useTestNG()
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }
        }
    }

    task uploadInternal(type: Upload) {
        configuration = configurations.archives
        uploadDescriptor = true
        repositories {
           mavenDeployer {
               repository(url: nexusReleaseUrl) {
                  authentication(userName: nexusReleaseUsername, password: nexusReleasePassword)
               }
               snapshotRepository(url: nexusSnapshotUrl) {
                   authentication(userName: nexusSnapshotUsername, password: nexusSnapshotPassword)
               }
           }
        }
    }

    task uploadLocal(type: Upload) {
        configuration = configurations.archives
        uploadDescriptor = true
        repositories {
            mavenLocal()
        }
    }

}
